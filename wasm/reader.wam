(module $reader

  ;; TODO: global warning
  (global $token       (mut i32) 0)
  (global $read_index  (mut i32) 0)

  (func $skip_spaces (param $str i32) (result i32)
    (local $found i32)
    (local $c i32)
    (set_local $found 0)
    (set_local $c (i32.load8_u (i32.add $str (get_global $read_index))))
    (block $done
      (loop $loop
        ;;; while (c == ' ' || c == ',' || c == '\n')
        (if (i32.and (i32.and
                       (i32.ne $c (CHR " "))
                       (i32.ne $c (CHR ",")))
                     (i32.ne $c (CHR "\n")))
          (br $done))
        (set_local $found 1)
        ;;; c=str[++(*index)]
        (set_global $read_index (i32.add (get_global $read_index) 1))
        (set_local $c (i32.load8_u (i32.add $str (get_global $read_index))))
        (br $loop)
      )
    )
;;    ($debug ">>> skip_spaces:" $found)
    $found
  )

  (func $skip_to_eol (param $str i32) (result i32)
    (local $found i32)
    (local $c i32)
    (set_local $found 0)
    (set_local $c (i32.load8_c (i32.add $str (get_global $read_index))))
    (if (i32.eq $c (CHR ";"))
      (then
        (set_local $found 1)
        (block $done
          (loop $loop
            ;;; c=str[++(*index)]
            (set_global $read_index (i32.add (get_global $read_index) 1))
            (set_local $c (i32.load8_u (i32.add $str
                                                (get_global $read_index))))
            ;;; while (c != '\0' && c != '\n')
            (if (i32.and (i32.ne $c (CHR "\x00")) (i32.ne $c (CHR "\n")))
              (br $loop))
          )
        )))
;;    ($debug ">>> skip_to_eol:" $found)
    $found
  )

  (func $skip_spaces_comments (param $str i32)
    (loop $loop
      ;; skip spaces
      (if ($skip_spaces $str) (br $loop))
      ;; skip comments
      (if ($skip_to_eol $str) (br $loop))
    )
  )

  (func $read_token (param $str i32) (result i32)
    (local $token_index i32)
    (local $instring i32)
    (local $escaped i32)
    (local $c i32)
    (set_local $token_index 0)
    (set_local $instring 0)
    (set_local $escaped 0)

    ($skip_spaces_comments $str)

    ;; read first character
    ;;; c=str[++(*index)]
    (set_local $c (i32.load8_u (i32.add $str (get_global $read_index))))
    (set_global $read_index (i32.add (get_global $read_index) 1))
    ;; read first character
    ;;; token[token_index++] = c
    (i32.store8_u (i32.add (get_global $token) $token_index) $c)
    (set_local $token_index (i32.add $token_index 1))
    ;; single/double character token
    (if (i32.or (i32.eq $c (CHR "("))
        (i32.or (i32.eq $c (CHR ")"))
        (i32.or (i32.eq $c (CHR "["))
        (i32.or (i32.eq $c (CHR "]"))
        (i32.or (i32.eq $c (CHR "{"))
        (i32.or (i32.eq $c (CHR "}"))
        (i32.or (i32.eq $c (CHR "'"))
        (i32.or (i32.eq $c (CHR "`"))
        (i32.or (i32.eq $c (CHR "@"))
        (i32.and (i32.eq $c (CHR "~"))
                 (i32.eq (i32.load8_u (i32.add $str (get_global $read_index)))
                         (CHR "@"))))))))))))

      (then
        ;; continue
        (nop))
      (else
        ;;; if (c == '"') instring = true
        (set_local $instring (i32.eq $c (CHR "\"")))
        (block $done
          (loop $loop
            ;; peek at next character
            ;;; c = str[*index]
            (set_local $c (i32.load8_u
                            (i32.add $str (get_global $read_index))))
            ;;; if (c == '\0') break
            (if (i32.eq $c 0) (br $done))
            ;;; if (!instring)
            (if (i32.eqz $instring)
              (then
                ;; next character is token delimiter
                (if (i32.or (i32.eq $c (CHR "("))
                    (i32.or (i32.eq $c (CHR ")"))
                    (i32.or (i32.eq $c (CHR "["))
                    (i32.or (i32.eq $c (CHR "]"))
                    (i32.or (i32.eq $c (CHR "{"))
                    (i32.or (i32.eq $c (CHR "}"))
                    (i32.or (i32.eq $c (CHR " "))
                    (i32.or (i32.eq $c (CHR ","))
                            (i32.eq $c (CHR "\n"))))))))))
                  (br $done))))
            ;; read next character
            ;;; token[token_index++] = str[(*index)++]
            (i32.store8_u (i32.add (get_global $token) $token_index)
                          (i32.load8_u
                            (i32.add $str (get_global $read_index))))
            (set_local $token_index (i32.add $token_index 1))
            (set_global $read_index (i32.add (get_global $read_index) 1))
            ;;; if (token[0] == '~' && token[1] == '@') break
            (if (i32.and (i32.eq (i32.load8_u (i32.add (get_global $token) 0))
                                 (CHR "~"))
                         (i32.eq (i32.load8_u (i32.add (get_global $token) 1))
                                 0x40))
              (br $done))

            ;;; if ((!instring) || escaped)
            (if (i32.or (i32.eqz $instring) $escaped)
              (then
                (set_local $escaped 0)
                (br $loop)))
            (if (i32.eq $c (CHR "\\"))
              (set_local $escaped 1))
            (if (i32.eq $c (CHR "\""))
              (br $done))
            (br $loop)
          )
        )))

    ;;; token[token_index] = '\0'
    (i32.store8_u (i32.add (get_global $token) $token_index) 0)
    (get_global $token)
  )

  (func $read_seq (param $str i32) (param $type i32) (param $end i32)
        (result i32)
    (local $res i32)
    (local $val2 i32)
    (local $val3 i32)
    (local $c i32)

    ;; MAP_LOOP stack
    (local $ret i32)
    (local $empty i32)
    (local $current i32)

    ;; MAP_LOOP_START
    (set_local $res ($MAP_LOOP_START $type))
    ;; push MAP_LOOP stack
    ;;; empty = current = ret = res
    (set_local $ret $res)
    (set_local $current $res)
    (set_local $empty $res)

    ;; READ_SEQ_LOOP
    (block $done
      (loop $loop
        ($skip_spaces_comments $str)

        ;; peek at next character
        ;;; c = str[*index]
        (set_local $c (i32.load8_u (i32.add $str (get_global $read_index))))
        (if (i32.eq $c (CHR "\x00"))
          (then
            ($THROW_STR_0 "unexpected EOF")
            (br $done)))
        (if (i32.eq $c $end)
          (then
            ;; read next character
            ;;; c = str[(*index)++]
            (set_local $c (i32.load8_u (i32.add $str (get_global $read_index))))
            (set_global $read_index (i32.add (get_global $read_index) 1))
            (br $done)))

        ;; value (or key for hash-maps)
        (set_local $val2 ($read_form $str))

        ;; if error, release the unattached element
        (if (get_global $error_type)
          (then
            ($RELEASE $val2)
            (br $done)))

        ;; if this is a hash-map, READ_FORM again
        (if (i32.eq $type (get_global $HASHMAP_T))
          (set_local $val3 ($read_form $str)))

        ;; update the return sequence structure
        ;; MAP_LOOP_UPDATE
        (set_local $res ($MAP_LOOP_UPDATE $type $empty $current $val2 $val3))
        (if (i32.le_u $current (get_global $EMPTY_HASHMAP))
          ;; if first element, set return to new element
          (set_local $ret $res))
        ;; update current to point to new element
        (set_local $current $res)

        (br $loop)
      )
    )

    ;; MAP_LOOP_DONE
    $ret
  )

  (func $read_form (param $str i32) (result i32)
    ;;($STRING (get_global $STRING_T) $str)
    (local $tok i32)
    (local $c0 i32)
    (local $c1 i32)
    (local $res i32)

    (if (get_global $error_type) (return 0))

    (set_local $tok ($read_token $str))
;;    ($debug ">>> read_form 1:" $tok)
    ;;; c0 = token[0]
    (set_local $c0 (i32.load8_u $tok))
    (set_local $c1 (i32.load8_u (i32.add $tok 1)))

    (if (i32.eq $c0 0)
      (then
        (return ($INC_REF (get_global $NIL))))
      (else (if (i32.or (i32.and (i32.ge_u $c0 (CHR "0"))
                                 (i32.le_u $c0 (CHR "9")))
                        (i32.and (i32.eq $c0 (CHR "-"))
                                 (i32.and (i32.ge_u $c1 (CHR "0"))
                                          (i32.le_u $c1 (CHR "9")))))
      (then
        (return ($INTEGER ($ATOI $tok))))
    (else (if (i32.eq $c0 (CHR ":"))
      (then
        (i32.store8_u $tok (CHR "\x7f"))
        (return ($STRING (get_global $STRING_T) $tok)))
    (else (if (i32.eq $c0 (CHR "\""))
      (then
        ;; TODO: unescape
        (i32.store8_u (i32.sub_u (i32.add $tok ($STRING_LEN $tok)) 1)
                      (CHR "\x00"))
        (return ($STRING (get_global $STRING_T) (i32.add $tok 1))))
    (else (if (i32.eq $c0 (CHR "("))
      (then
        (return ($read_seq $str (get_global $LIST_T) (CHR ")"))))
    (else (if (i32.eq $c0 (CHR "["))
      (then
        (return ($read_seq $str (get_global $VECTOR_T) (CHR "]"))))
    (else (if (i32.eq $c0 (CHR "{"))
      (then
        (return ($read_seq $str (get_global $HASHMAP_T) (CHR "}"))))
    (else (if (i32.or (i32.eq $c0 (CHR ")"))
                      (i32.or (i32.eq $c0 (CHR "]"))
                              (i32.eq $c0 (CHR "}"))))
      (then
        ($THROW_STR_1 "unexpected '%c'" $c0)
        (return 0))
    (else
      (return ($STRING (get_global $SYMBOL_T) $tok))))))))))))))))))
  )

  (func $read_str (param $str i32) (result i32)
    (set_global $read_index 0)
    ($read_form $str)
  )

  (export "read_str" (func $read_str))

)
