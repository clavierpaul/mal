(module $env

  (func $ENV_NEW (param $outer i32) (result i32)
    (local $data i32)
    (local $env i32)

    ;; allocate the data hashmap
    (set_local $data ($HASHMAP))

    (set_local $env ($ALLOC (get_global $ENVIRONMENT_T) $data $outer 0))
    ;; environment takes ownership
    ($RELEASE $data)
    $env
  )

  (func $ENV_SET (param $env i32) (param $key i32) (param $value i32)
        (result i32)
    (local $data i32)
    (set_local $data ($MEM_VAL0_ptr $env))
    (i32.store ($VAL0_ptr $env) ($MalVal_index ($ASSOC1 $data $key $value)))
    $value
  )

  (func $ENV_SET_S (param $env i32) (param $key i32) (param $value i32)
        (result i32)
    (local $data i32)
    (set_local $data ($MEM_VAL0_ptr $env))
    (i32.store ($VAL0_ptr $env) ($MalVal_index ($ASSOC1_S $data $key $value)))
    $value
  )

  (func $ENV_FIND (param $env i32) (param $key i32) (result i64)
    (local $res i32)
    (local $data i32)
    (local $found_res i64)

    (set_local $res 0)

    (block $done
      (loop $loop
        (set_local $data ($MEM_VAL0_ptr $env))
        (set_local $found_res ($HASHMAP_GET $data
                                    $key))
        ;;; if (found)
        (if (i32.wrap/i64 (i64.shr_u $found_res (i64.const 32)))
          (then
            (set_local $res (i32.wrap/i64 $found_res))
            (br $done)))
        (set_local $env ($MEM_VAL1_ptr $env))
        (if (i32.eq $env (get_global $NIL))
          (then
            (set_local $env 0)
            (br $done)))
        (br $loop)
      )
    )

    ;; combine res/env as hi 32/low 32 of i64
    (i64.or
      (i64.shl_u (i64.extend_u/i32 $res) (i64.const 32))
      (i64.extend_u/i32 $env))
  )

  (func $ENV_GET (param $env i32) (param $key i32) (result i32)
    (local $res i32)
    (local $res_env i64)
    (set_local $res 0)

    (set_local $res_env ($ENV_FIND $env $key))
    (set_local $env (i32.wrap/i64 $res_env))
    (set_local $res (i32.wrap/i64 (i64.shr_u $res_env (i64.const 32))))

    (if (i32.eqz $env)
      (then
        ($THROW_STR_1 "'%s' not found" ($to_String $key))
        (return $res)))
    (return ($INC_REF $res))
  )
)
