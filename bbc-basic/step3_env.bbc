REM Step 3 of mal in BBC BASIC

LIBRARY "malio_builtin"
LIBRARY "types"
LIBRARY "reader"
LIBRARY "printer"
LIBRARY "env"

PROCmalio_init
PROCtypes_init

REM  These correspond with the CASE statement in FNcore_call
repl_env% = FNalloc_environment(FNnil)
PROCenv_set(repl_env%, FNalloc_symbol("+"), FNalloc_corefn(0))
PROCenv_set(repl_env%, FNalloc_symbol("-"), FNalloc_corefn(1))
PROCenv_set(repl_env%, FNalloc_symbol("*"), FNalloc_corefn(2))
PROCenv_set(repl_env%, FNalloc_symbol("/"), FNalloc_corefn(3))

REPEAT
  REM  Catch all errors apart from "Escape".
  ON ERROR LOCAL PRINT REPORT$:IF ERR = 17 THEN END
  line$ = FNmalio_input("user> ")
  PROCmalio_println(FNrep(line$))
UNTIL FNmalio_eof

END

DEF FNREAD(a$)
=FNread_str(a$)

DEF FNEVAL(ast%, env%)
  LOCAL args%(), car%
  IF NOT FNis_list(ast%) THEN =FNeval_ast(ast%, env%)
  IF FNis_empty(ast%) THEN =ast%
  car% = FNlist_car(ast%)
  IF FNis_symbol(car%) THEN
    CASE FNunbox_symbol(car%) OF
      REM  Special forms
      WHEN "def!"
        LOCAL val%
	val% = FNEVAL(FNlist_nth(ast%, 3), env%)
        PROCenv_set(env%, FNlist_nth(ast%, 2), val%)
	=val%
      WHEN "let*"
        LOCAL bindings%
	env% = FNalloc_environment(env%)
	bindings% = FNlist_nth(ast%, 2)
	WHILE NOT FNis_empty(bindings%)
	  PROCenv_set(env%, FNlist_car(bindings%), FNEVAL(FNlist_nth(bindings%, 2), env%))
	  bindings% = FNlist_cdr(FNlist_cdr(bindings%))
	ENDWHILE
	=FNEVAL(FNlist_nth(ast%, 3), env%)
    ENDCASE
  ENDIF
  ast% = FNeval_ast(ast%, env%)
  DIM args%(FNlist_len(FNlist_cdr(ast%)))
  PROClist_to_array(FNlist_cdr(ast%), args%())
=FNcore_call(FNunbox_corefn(FNlist_car(ast%)), args%())

DEF FNPRINT(a%)
=FNpr_str(a%)

DEF FNrep(a$)
=FNPRINT(FNEVAL(FNREAD(a$), repl_env%))

DEF FNeval_ast(ast%, env%)
  LOCAL val%, car%, cdr%
  IF FNis_symbol(ast%) THEN =FNenv_get(env%, ast%)
  IF FNis_list(ast%) THEN
    IF FNis_empty(ast%) THEN =ast%
    car% = FNEVAL(FNlist_car(ast%), env%)
    cdr% = FNeval_ast(FNlist_cdr(ast%), env%)
    =FNalloc_pair(car%, cdr%)
  ENDIF
=ast%

REM  Call a core function, taking the function number and an array of mal
REM  objects to pass as arguments.
DEF FNcore_call(fn%, args%())
  CASE fn% OF
    WHEN 0 : =FNalloc_int(FNunbox_int(args%(0))  +  FNunbox_int(args%(1)))
    WHEN 1 : =FNalloc_int(FNunbox_int(args%(0))  -  FNunbox_int(args%(1)))
    WHEN 2 : =FNalloc_int(FNunbox_int(args%(0))  *  FNunbox_int(args%(1)))
    WHEN 3 : =FNalloc_int(FNunbox_int(args%(0)) DIV FNunbox_int(args%(1)))
  ENDCASE
ERROR &40E809F1, "Call to non-existent core function"
