REM > env library for mal in BBC BASIC

DEF FNnew_env(outer%, binds%, exprs%)
  LOCAL env%
  env% = FNalloc_environment(outer%)
  WHILE NOT FNis_empty(binds%)
    IF FNunbox_symbol(FNlist_car(binds%)) = "&" THEN
      PROCenv_set(env%, FNlist_nth(binds%, 1), exprs%)
      binds% = FNempty
    ELSE
      PROCenv_set(env%, FNlist_car(binds%), FNlist_car(exprs%))
      binds% = FNlist_cdr(binds%) : exprs% = FNlist_cdr(exprs%)
    ENDIF
  ENDWHILE
=env%

DEF PROCenv_set(env%, keysym%, val%)
  LOCAL data%
  data% = FNenvironment_data(env%)
  data% = FNalloc_hashmap_entry(FNunbox_symbol(keysym%), val%, data%)
  PROCenvironment_set_data(env%, data%)
ENDPROC

DEF FNenv_find(env%, keysym%)
  LOCAL val%, outer%, key$
  key$ = FNunbox_symbol(keysym%)
  IF FNhashmap_contains(FNenvironment_data(env%), key$) THEN =env%
  outer% = FNenvironment_outer(env%)
  IF FNis_nil(outer%) THEN =FNnil
=FNenv_find(outer%, keysym%)

DEF FNenv_get(env%, keysym%)
  LOCAL key$
  env% = FNenv_find(env%, keysym%)
  key$ = FNunbox_symbol(keysym%)
  IF FNis_nil(env%) THEN ERROR &40E80922, "'"+key$+"' not found"
=FNhashmap_get(FNenvironment_data(env%), key$)

REM Local Variables:
REM indent-tabs-mode: nil
REM End:
