REM > env library for mal in BBC BASIC

DEF FNnew_env(outer%, binds%, exprs%)
  LOCAL env%
  env% = FNalloc_environment(outer%)
  WHILE NOT FNis_empty(binds%)
    PROCenv_set(env%, FNlist_car(binds%), FNlist_car(exprs%))
    binds% = FNlist_cdr(binds%) : exprs% = FNlist_cdr(exprs%)
  ENDWHILE
=env%

DEF PROCenv_set(env%, keysym%, val%)
  LOCAL data%
  data% = FNenvironment_data(env%)
  data% = FNalloc_hashmap_entry(FNunbox_symbol(keysym%), val%, data%)
  PROCenvironment_set_data(env%, data%)
ENDPROC

DEF FNenv_find(env%, keysym%)
  LOCAL val%, outer%
  val% = FNhashmap_get(FNenvironment_data(env%), FNunbox_symbol(keysym%))
  IF NOT FNis_nil(val%) THEN =env%
  outer% = FNenvironment_outer(env%)
  IF NOT FNis_nil(outer%) THEN =FNenv_find(outer%, keysym%)
=FNnil

DEF FNenv_get(env%, keysym%)
  env% = FNenv_find(env%, keysym%)
  IF FNis_nil(env%) THEN
    ERROR &40E80922, "Symbol '"+FNunbox_symbol(keysym%)+"' not found"
  ENDIF
=FNhashmap_get(FNenvironment_data(env%), FNunbox_symbol(keysym%))

REM Local Variables:
REM indent-tabs-mode: nil
REM End:
